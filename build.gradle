plugins {
    id "org.springframework.boot" version "2.1.4.RELEASE"
    id 'application'
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

repositories {
    jcenter()
}

configurations {
    compile.exclude module: 'spring-boot-starter-tomcat'
}

dependencies {
    // for type inference
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    compileOnly 'org.projectlombok:lombok:1.18.6'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'
    testCompileOnly 'org.projectlombok:lombok:1.18.6'

    // for logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    // dependencies by requiring openapi-generator generated code
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.8'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'

    // spring boot with jetty
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: '2.1.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.1.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.4.RELEASE'

    // openapi generator
    //compile group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.1.0'
    //swaggerCodegen 'org.openapitools:openapi-generator-cli:4.0.0-beta3'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'

    implementation 'com.google.guava:guava:27.0.1-jre'
    testImplementation 'junit:junit:4.12'
}

swaggerSources {
    openapi {
        inputFile = file('spec/openapi.yaml')
        code {
            language = 'spring'
            configFile = file('spec/config.json')
            // dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.openapi.code
sourceSets.main.java.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/resources"

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.example.springboot.ServerMain'

// for debugging
tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
}
